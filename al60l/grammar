Program ::=
	| CompoundStatement

CompoundStatement ::=
	| LabelList Block

LabelList ::=
	| <eps>
	| Label LabelList

Label ::=
	| LabelIdentifier SEPCOLON

LabelIdentifier ::=
	| IDENTIFIER
	| LITINTEGER

Block ::=
	| KWBEGIN DeclarationList StatementList KWEND

DeclarationList ::=
	| <eps>
	| Declaration SEPSEMICOLON DeclarationList

StatementList ::=
	| Statement SEPSEMICOLON StatementList
	| Statement

Statement ::=
	| UnconditionalStatement
	| LabelList IfStatement
	| LabelList ForStatement

UnconditionalStatement ::=
	| CompoundStatement
	| BasicStatement

BasicStatement ::=
	| LabelList DummyStatement
	| LabelList AssignmentStatement
	| LabelList GotoStatement
	| LabelList Expression    # note: only procedure calls allowed

DummyStatement ::=
	| <eps>

AssignmentStatement ::=
	| TargetVariableList Expression

TargetVariableList ::=
	| TargetVariable TargetVariables
	| TargetVariable

TargetVariable ::=
	| IDENTIFIER SEPASSIGN

GotoStatement ::=
	| KWGOTO DesignationalExpression

IfStatement ::=
	| IfClause UnconditionalStatement OptElseClause
	| IfClause ForStatement

IfClause ::=
	| KWIF Expression KWTHEN

OptElseClause ::=
	| <eps>
	| KWELSE Statement

ForStatement ::=
	| KWFOR IDENTIFIER SEPASSIGN ForList KWDO Statement

ForList ::=
	| ForListElement
	| ForList SEPCOMMA ForListElement

ForListElement ::=
	| Expression
	| Expression KWSTEP Expression KWUNTIL Expression
	| Expression KWWHILE Expression
	# note: check if expression types match!
	# note: not sure if it's possible to use float expressions
